title: Foreign Policy
new-page: true
is-card: true
on-arrival: foreign_policy_timer += 4; week_actions += 1
view-if: foreign_policy_timer == 0 and ((player_party = "k" and k_in_gov and (foreign_minister_party = "Kadet" or foreign_minister_party = "Independent" or foreign_minister_party = "Progressive" or foreign_minister_party = "Octobrist" or foreign_minister_party = "Trudovik" or foreign_minister_party = "Popular Socialist")) or (player_party = "sr" and sr_in_gov and (foreign_minister_party = "SR" or foreign_minister_party = "Trudovik" or foreign_minister_party = "Popular Socialist")) or (player_party = "m" and m_in_gov and foreign_minister_party = "Menshevik"))
card-image: img/america.jpg
tags: prov_gov

= Foreign Policy

What should our foreign policy priority be?

# negotiate for a general peace with all powers
# negotiate separate ceasefire with central powers
# negotiate for continued financial support from the entente
# improve relations with the entente
# improve relations with the US
# negotiate for financial support from the US

- @improve_west
#- @improve_us
- @loans
- @peace_negotiate
- @peace_negotiate_2
- @peace_negotiate_3
- @sign_peace
- @root: No new policy is needed.
- @easy_discard

@improve_west
title: Improve our relations with our allies in the Entente, France and Britain.
on-arrival: west_relation += 6*prov_gov_efficacy; west_relation -= 5*prov_gov_efficacy if central_negotiations > 0 or is_armistice

We are working to improve relations with France and Britain. [? if central_negotiations > 0 or is_armistice: Unfortunately, given our seeking of a separate peace, the Allies are much less inclined to treat us favorably. ?]

@improve_us
title: Improve relations with the United States.

@loans
title: Negotiate for new loans.
subtitle: +2 budget; this will lower relations with the West.
unavailable-subtitle: We do not have good enough relations with the Allies.
choose-if: west_relation >= 55
on-arrival: west_relation -= 8; budget += 2;

We have gotten new loans from the allies.

@technical_assistance
title: Negotiate for more technical assistance.
unavailable-subtitle: We do not have good enough relations with the Allies.
choose-if: west_relation >= 55

Unfortunately, all foreign shipments must go through either Vladivostok in the Far East, where it has to be transported via the rickety Trans-Siberian Railroad, or Arkhangel'sk in the far north, where there is only a single train line leading to central Russia.

@trains
title: Purchase train cars.

@industry
title: Purchase industrial equipment.

@peace_negotiate
title: Negotiate for a ceasefire with the Central Powers.
choose-if: (player_party = "m" and m_war_policy >= 2) or (player_party = "sr" and sr_war_policy >= 2) or (player_party = "k" and k_war_policy >= 1)
unavailable-subtitle: Our party does not support this.
view-if: armistice_progress = 0
on-arrival: armistice_progress += 1; west_relation -= 10;

We have opened ceasefire negotiations with the Central Powers. We should be able to sign a ceasefire soon...

@peace_negotiate_2
title: Sign a ceasefire with the Central Powers.
choose-if: (player_party = "m" and m_war_policy >= 2) or (player_party = "sr" and sr_war_policy >= 2) or (player_party = "k" and k_war_policy >= 1)
unavailable-subtitle: Our party does not support this.
view-if: armistice_progress = 1 and is_armistice = 0
on-arrival: armistice_progress += 1; west_relation -= 30; is_armistice = 1; prov_gov_legitimacy += 25; war_or_peace_timer = 8; soldiers_b -= 8 if player_party != "b"; soldiers_m += 8*(1-m_dissent) if m_in_gov; soldiers_sr += 8*(1-sr_dissent) if sr_in_gov; soldiers_k += 6*(1-k_dissent) if k_in_gov; central_negotiations = 1; discipline *= 0.8; morale *= 0.8; war_weariness -= 10
achievement: peace

# begin timers - war or peace: either peace treaty or war begins anew.

We have signed an armistice with the Central Powers! It is only a temprary measure; we still must negotiate a permanent peace treaty. If we do not negotiate a peace treaty in time, the Germans might resume their attack.

Our relations with the West might be irreparable after this.

@peace_negotiate_3
title: Negotiate for a permanent peace treaty.
view-if: is_armistice and central_negotiations < 2 and not is_peace
on-arrival: central_negotiations += 1; war_or_peace_timer += 2;

We are continuing with the negotiations for a permanent peace.

[? if war_progress >= -2 and war_progress <= 0 : The war has had a mix of outcomes so far, with advances along some fronts and retreats along others, but with more defeats than victories for Russia. We will have to make territorial concessions for a peace. ?]
[? if war_progress < -2 and war_progress > -5 : Given our defeats in the war, we will likely have to make major territorial concessions in the peace. ?]
[? if war_progress <= -5 : Given our severe defeats in the war, there will be major territorial concessions in any peace treaty.  ?]
[? if war_progress > 0 : On the whole, we have an advantageous position in the negotiations, given our victories in the war. ?]


@sign_peace
title: We are ready for a peace treaty.
view-if: central_negotiations >= 2 and not is_peace
go-to: treaty_good if war_progress > 0; treaty_neutral if war_progress = 0; treaty_bad if war_progress < 0 and war_progress >= -2; treaty_harsh if war_progress < -2 and war_progress >= -4; treaty_severe if war_progress < -4

# notes on potential peace treaties: https://encyclopedia.1914-1918-online.net/article/peace-initiatives-1-1/ - see the section on the Erzberger-Kolyshko talks.

@treaty_good
on-arrival: peace_terms = 1

= A good treaty

The treaty is largely on good terms for us. There are no indemnities on either side. [? if caucasus_progress > 0 : There will be some beneficial border adjustments with the Ottoman Empire in the Caucasus region. ?] [? if southwestern_progress > 0 : We will make some gains in the Galician (Southwestern) Front. ?]

- @sign_treaty_good
- @renegotiate

@sign_treaty_good
title: Sign the peace treaty!
on-arrival: is_peace = 1; prov_gov_legitimacy += 10; middle_m += 5*(1-m_dissent) if m_in_gov; middle_sr += 5*(1-sr_dissent) if sr_in_gov; middle_k += 10*(1-k_dissent) if k_in_gov; workers_m += 8*(1-m_dissent) if m_in_gov; workers_sr += 8*(1-sr_dissent) if sr_in_gov; workers_b += 10*(1-b_dissent) if b_in_gov; soldiers_m += 10*(1-m_dissent) if m_in_gov; soldiers_sr += 10*(1-sr_dissent) if sr_in_gov; soldiers_b += 15*(1-b_dissent) if b_in_gov; war_weariness -= 25
achievement: peace_2

We have officially signed a separate peace with the Central Powers!


@treaty_neutral
on-arrival: peace_terms = 0;

= A relatively neutral treaty

There are no indemnities in the treaty. Poland and Finland will become independent states, but there are few other territorial changes. [? if caucasus_progress > 0 : There will be some beneficial border adjustments with the Ottoman Empire in the Caucasus region. ?]

- @sign_treaty_neutral: Sign the peace treaty!
- @refuse_treaty
- @renegotiate: Can we renegotiate the peace treaty?

@sign_treaty_neutral
title: Sign the peace treaty!
on-arrival: is_peace = 1; prov_gov_legitimacy += 5; middle_m += 5*(1-m_dissent) if m_in_gov; middle_sr += 5*(1-sr_dissent) if sr_in_gov; middle_k += 5*(1-k_dissent) if k_in_gov; workers_m += 5*(1-m_dissent) if m_in_gov; workers_sr += 5*(1-sr_dissent) if sr_in_gov; workers_b += 5*(1-b_dissent) if b_in_gov; soldiers_m += 5*(1-m_dissent) if m_in_gov; soldiers_sr += 5*(1-sr_dissent) if sr_in_gov; soldiers_b += 5*(1-b_dissent) if b_in_gov; war_weariness -= 25
achievement: peace_2

We have officially signed a separate peace with the Central Powers!

@treaty_bad
on-arrival: peace_terms = -1

= A difficult treaty

The treaty involves a loss of territory to the Central Powers, but no indemnities. Finland will become an independent state.
[? if northern_progress < 0 : The Baltic states of Lithuania, Latvia, and Estonia will become independent, likely under the German sphere of influence. ?]
[? if western_progress < 0: Poland will be an independent state, and will be part of the German sphere of influence. ?]

[? if southwestern_progress > 0 : We will give up our gains in the Galician (Southwestern) Front. ?]

[? if caucasus_progress > 0 : There will be some beneficial border adjustments with the Ottoman Empire in the Caucasus region. ?]

- @sign_treaty_bad
- @renegotiate


@sign_treaty_bad
title: Sign the peace treaty!
on-arrival: is_peace = 1; west_relation -= 10; prov_gov_legitimacy -= 5; m_k_relation -= 10 if m_in_gov; sr_k_relation -= 10 if k_in_gov; b_k_relation -= 20 if b_in_gov; industry -= 5; train_capacity *= 0.95; war_weariness -= 25
achievement: peace_2

We have officially signed a separate peace with the Central Powers! The treaty is viewed negatively by the Kadets and those who sympathize most with the Allies, but it is generally accepted.

@treaty_harsh
on-arrival: peace_terms = -2;

= A harsh treaty

The treaty involves a loss of territory to the Central Powers. In addition, there will be some indemnity payments. Finland will become an independent state.
[? if northern_progress < 0 : The Baltic states of Lithuania, Latvia, and Estonia will become independent, likely under the German sphere of influence. ?]
[? if western_progress < 0: Poland will be an independent state, and will be part of the German sphere of influence. We will also lose some adjacent territory inhabited mostly by Belarussians and Ukrainians. ?]

[? if southwestern_progress > 0 : We will give up our gains in the Galician (Southwestern) Front. ?]
[? if southwestern_progress <= 0 : We will give up some territory in western Ukraine. ?]

[? if caucasus_progress > 0 : We will give up our gains in the Caucasus region. ?]
[? if caucasus_progress <= 0 : We will lose some territory to the Ottoman Empire in the Caucasus region. ?]

Many Russians view the treaty as humiliating, but some see it as necessary to achieve a peace. A significant amount of our industrial production, agricultural resources, and population will be given to Germany.

- @sign_treaty_harsh
- @renegotiate


@sign_treaty_harsh
title: Sign the peace treaty!
subtitle: There is no other option.
on-arrival: is_peace = 1; west_relation -= 10; prov_gov_legitimacy -= 10; m_k_relation -= 20 if m_in_gov; sr_k_relation -= 20 if k_in_gov; b_k_relation -= 20 if b_in_gov; budget -= 2; industry -= 10; food_production *= 0.9; urban_population *= 0.9; rural_population *= 0.9; train_capacity *= 0.9; workers_m -= 10 if m_in_gov; workers_sr -= 10 if sr_in_gov; rural_sr -= 10 if sr_in_gov; workers_b -= 10 if b_in_gov; middle_k -= 5 if k_in_gov; middle_m -= 10 if m_in_gov; middle_sr -= 10 if sr_in_gov; war_weariness -= 20
achievement: peace_2

We have officially signed a separate peace with the Central Powers! The treaty is viewed as harsh and humiliating in many parts of Russia, and the popularity of all of the government parties has declined.



@treaty_severe
on-arrival: peace_terms = -3; prov_gov_legitimacy -= 5;

= A severe treaty

The mere proposal of the treaty is severe enough to cause a backlash in public opinion. The treaty involves a great deal of loss of territory to the Central Powers, as well as indemnity payments of food and resources.

Finland will become an independent state.
[? if northern_progress < 0 : The Baltic states of Lithuania, Latvia, and Estonia will become independent under the German sphere of influence, likely to become German puppet states. ?]
[? if western_progress < 0: Poland will be an independent state, and will be part of the German sphere of influence. We will also lose some adjacent territory inhabited mostly by Belarussians and Ukrainians, to be controlled by German puppet states. ?]

[? if southwestern_progress > 0 : We will give up our gains in the Galician (Southwestern) Front. ?]
[? if southwestern_progress <= 0 : Most of Ukrainian territory will become nominally independent under German influence. ?]

[? if caucasus_progress > 0 : We will give up our gains in the Caucasus region. ?]
[? if caucasus_progress <= 0 : We will lose some territory to the Ottoman Empire in the Caucasus region. ?]

Signing this treaty will likely lead to rebellion, not just among the right. A huge proportion of our industrial production, food production, and population will be given to Germany.

- @sign_treaty_severe
- @renegotiate
- @refuse_treaty


@sign_treaty_severe
title: Sign the peace treaty!
subtitle: There is no other option.
on-arrival: is_peace = 1; west_relation -= 10; prov_gov_legitimacy -= 10; m_k_relation -= 30 if m_in_gov; sr_k_relation -= 30 if k_in_gov; b_k_relation -= 50 if b_in_gov; budget -= 4; industry -= 35; food_production *= 0.7; urban_population *= 0.8; rural_population *= 0.8; train_capacity *= 0.8; workers_m -= 15 if m_in_gov; workers_sr -= 15 if sr_in_gov; rural_sr -= 15 if sr_in_gov; workers_b -= 15 if b_in_gov; middle_k -= 15 if k_in_gov; middle_m -= 15 if m_in_gov; middle_sr -= 15 if sr_in_gov; war_weariness -= 15
achievement: peace_2

We have officially signed a separate peace with the Central Powers! The treaty is viewed as harsh and humiliating in many parts of Russia, and the popularity of all of the government parties has declined. A significant portion of our land, population, and industrial capacity has been given up.


@renegotiate
title: Can we renegotiate?
subtitle: It is very unlikely that terms will improve; this is a stalling tactic.
unavailable-subtitle: The Central Powers are refusing to renegotiate.
choose-if: renegotiate = 0 and war_or_peace_seen = 0
on-arrival: central_negotiations = 1; renegotiate = 1; war_or_peace_timer += 4

We will attempt to renegotiate the peace treaty for better terms, but it is very unlikely that the Central Powers will give us better terms.

@refuse_treaty
title: Refuse to sign the peace treaty!
subtitle: This will cause the resumption of war.
view-if: renegotiate > 0 or war_or_peace_seen > 0
on-arrival: is_peace = 0; is_armistice = 0; central_negotiations = 0; refuse_treaty = 1; german_offensive_timer = 0; armistice_progress = 0
achievement: no_peace_no_war

We have refused to sign the treaty. War has resumed with the Central Powers.

# TODO: forced negotiations...
